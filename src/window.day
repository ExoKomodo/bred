import "src/types.day"

importc "./types.h"
importc "./window.h"

importc "string.h"
importc "sys/ioctl.h"
importc "unistd.h"

fun bred_buffer_clear_screen (config: BreditorConfig) BreditorConfig do
  let cleared_buffer: String <- (string_append_fast config.buffer (string_from_cstring "\x1b[2J"))
  (bred_buffer_reset_cursor (bred_breditor_config_with_buffer config cleared_buffer))
end

fun bred_clear_screen () int do
  let sequence <- "\x1b[2J"
  match
    (truthy
      (write
        STDOUT_FILENO
        sequence
        (strlen sequence)))
      => (bred_reset_cursor)
    (default)
      => 1
  end
end

fun bred_get_window_data () BredWindowData do
  mut window_size_ptr: ptr<winsize> <- (malloc (sizeof winsize))
  let result: int <- (ioctl STDOUT_FILENO TIOCGWINSZ window_size_ptr)
  let window_size: winsize <- (deref window_size_ptr)
  match
    (or
      (eq result (neg 1))
      (eq window_size.ws_col 0)) => do
      (delete window_size_ptr)
      {BredWindowData
        cols <- 0
        rows <- 0
      }
    end
    (default) => do
      let data <- {BredWindowData
        cols <- window_size.ws_col
        rows <- window_size.ws_row
      }
      (delete window_size_ptr)
      data
    end
  end
end

fun bred_buffer_reset_cursor (config: BreditorConfig) BreditorConfig do
  let reset_buffer: String <- (string_append_fast config.buffer (string_from_cstring "\x1b[H"))
  (bred_breditor_config_with_buffer config reset_buffer)
end


fun bred_reset_cursor () int do
  let sequence <- "\x1b[H"
  match
    (truthy
      (write
        STDOUT_FILENO
        sequence
        (strlen sequence)))
      => 0
    (default)
      => 1
  end
end

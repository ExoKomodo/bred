import "src/types.day"

importc "./types.h"
importc "./window.h"

importc "string.h"
importc "sys/ioctl.h"
importc "unistd.h"

fun _bred_send_command(sequence: cstring) int do
  match
    (truthy
      (write
        STDOUT_FILENO
        sequence
        (strlen sequence)))
      => 0
    (default)
      => 1
  end
end

fun _bred_send_command_buffered(config: BreditorConfig sequence: ccstring) BreditorConfig do
  let buffer: String <- (string_append_fast config.buffer (string_from_ccstring sequence))
  (bred_breditor_config_with_buffer config buffer)
end

fun bred_clear_screen () int do
  (_bred_send_command "\x1b[2J")
end

fun bred_clear_screen_buffered (config: BreditorConfig) BreditorConfig do
  (_bred_send_command_buffered config "\x1b[2J")
end

fun bred_get_window_data () BredWindowData do
  mut window_size_ptr: ptr<winsize> <- (malloc (sizeof winsize))
  let result: int <- (ioctl STDOUT_FILENO TIOCGWINSZ window_size_ptr)
  let window_size: winsize <- (deref window_size_ptr)
  match
    (or
      (eq result (neg 1))
      (eq window_size.ws_col 0)) => do
      (delete window_size_ptr)
      {BredWindowData
        cols <- 0
        rows <- 0
      }
    end
    (default) => do
      let data <- {BredWindowData
        cols <- window_size.ws_col
        rows <- window_size.ws_row
      }
      (delete window_size_ptr)
      data
    end
  end
end

fun bred_hide_cursor () int do
  (_bred_send_command "\x1b[?25l")
end

fun bred_hide_cursor_buffered (config: BreditorConfig) BreditorConfig do
  (_bred_send_command_buffered config "\x1b[?25l")
end

fun bred_reset_cursor () int do
  (_bred_send_command "\x1b[H")
end

fun bred_reset_cursor_buffered (config: BreditorConfig) BreditorConfig do
  (_bred_send_command_buffered config "\x1b[H")
end

fun bred_show_cursor () int do
  (_bred_send_command "\x1b[?25h")
end

fun bred_show_cursor_buffered (config: BreditorConfig) BreditorConfig do
  (_bred_send_command_buffered config "\x1b[?25h")
end

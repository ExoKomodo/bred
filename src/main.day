import "src/commands.day"

importc "./helpers.h"
importc "./keyboard.h"
importc "./types.h"

importc "ctype.h"
importc "errno.h"
importc "stdlib.h"
importc "string.h"
importc "termios.h"
importc "unistd.h"

let QUIT <- 113
let ENTER <- 13

fun bred_display_rows (editor_config: EditorConfig row: size_t) int do
  let placeholder: cstring <- "~\r\n"
  match
    (gte row editor_config.number_of_rows)
      => 0
    (default) => match
      (truthy (write STDOUT_FILENO placeholder (strlen placeholder)))
        => (bred_display_rows editor_config (add row 1))
      (default)
        => (bred_dead "bred_display_rows" 1)
    end
  end
end

fun bred_display (editor_config: EditorConfig) int do
  (bred_display_rows editor_config 0)
  (bred_reset_cursor)
end

fun bred_loop (editor_config: EditorConfig) int do
  let c: char <- (bred_read_char editor_config)
  match
    (eq 0 (bred_clear_screen editor_config)) => match
      (eq (bred_process_char editor_config c) 0) => do
        (bred_display editor_config)
        (bred_loop editor_config)
      end
      (default)
        => 0
    end
    (default)
      => (bred_dead "bred_clear_screen" 1)
  end
end

fun bred_process_char (editor_config: EditorConfig c: char) int do
  (unused editor_config)
  match
    (falsey c)
      => 0
    (truthy (iscntrl c)) => match
      (eq c QUIT) => do
        (bred_clear_screen)
        1
      end
      (eq c ENTER) => do
        (printf "\r\n")
        0
      end
      (default) => do
        (printf "\r\nUnhandled control sequence: %d\r\n" c)
        0
      end
    end
    (eq c QUIT) => do
      (bred_clear_screen)
      1
    end
    (default) => do
      (printf "%c" c)
      (fflush stdout)
      0
    end
  end
end

fun bred_read_char (editor_config: EditorConfig) char do
  mut input: ptr<char> <- editor_config.input
  (memset input 0 1)
  let read_result: int <- (read STDIN_FILENO input 1)
  match
    (and
      (eq read_result (neg 1))
      (not (eq errno EAGAIN)))
      => (bred_dead "read" 1)
    (default)
      => (unsafe_index input 0)
  end
end

fun bred_setup () EditorConfig do
  mut editor_config <- {EditorConfig
    input <- (calloc (sizeof char) 1)
    number_of_rows <- 24
  }
  (bred_raw_enable editor_config)
end

fun main () int do
  mut editor_config: EditorConfig <- (bred_setup)
  let result: int <- (bred_loop editor_config)
  (empty_editor_config editor_config)
  result
end

importc "./helpers.h"
importc "./keyboard.h"
importc "./types.h"

importc "ctype.h"
importc "errno.h"
importc "stdlib.h"
importc "string.h"
importc "termios.h"
importc "unistd.h"

? q
let QUIT <- 113
let ENTER <- 13

fun bred_read_char (editor_config: EditorConfig) char do
  mut input: ptr<char> <- editor_config.input
  (memset input 0 1)
  let read_result: int <- (read STDIN_FILENO input 1)
  match
    (and
      (eq read_result (neg 1))
      (not (eq errno EAGAIN))) => do
      (bred_dead "read" 1)
      0
    end
    (default) => (unsafe_index input 0)
  end
end

fun bred_input_loop (editor_config: EditorConfig) int do
  let c: char <- (bred_read_char editor_config)
  match
    (falsey c) => (bred_input_loop editor_config)
    (truthy (iscntrl c)) => do
      match
        (eq c (BRED_CTRL_MOD QUIT)) => 0
        (eq c ENTER) => do
          (printf "\r\n")
          (bred_input_loop editor_config)
        end
        (default) => do
          (printf "\r\nUnhandled control sequence: %d\r\n" c)
          (bred_input_loop editor_config)
        end
      end
    end
    (default) => do
      (printf "%c" c)
      (fflush stdout)
      (bred_input_loop editor_config)
    end
  end
end

fun bred_setup () EditorConfig do
  mut editor_config <- {EditorConfig
    input <- (calloc (sizeof char) 1)
  }
  (bred_raw_enable editor_config)
end

fun main () int do
  mut editor_config: EditorConfig <- (bred_setup)
  let result: int <- (bred_input_loop editor_config)
  
  (empty_editor_config editor_config)
  (printf "\r\nI am bred\r\n")
  result
end

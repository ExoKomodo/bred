import "lib/string.day"

import "src/helpers.day"
import "src/types.day"
import "src/window.day"

importc "./keyboard.h"
importc "./raw.h"
importc "./types.h"

importc "ctype.h"
importc "errno.h"
importc "stdlib.h"
importc "string.h"
importc "termios.h"
importc "unistd.h"

let QUIT <- 113
let ENTER <- 13

fun bred_display_rows (config: BreditorConfig row: size_t) BreditorConfig do
  let line_placeholder <- "~\r\n"
  let last_line_placeholder <- "~"
  match
    (eq row (sub config.window_data.rows 1)) =>
      (bred_breditor_config_with_buffer
        config
        (string_append_fast config.buffer
          (string_from_ccstring last_line_placeholder)))
    (default) => do
      let new_config: BreditorConfig <- (bred_breditor_config_with_buffer
        config
        (string_append_fast config.buffer
          (string_from_ccstring line_placeholder)))
      (bred_display_rows new_config (add row 1))
    end
  end
end

fun bred_display (config: BreditorConfig) BreditorConfig do
  let new_config: BreditorConfig <-
    (bred_buffer_reset_cursor
      (bred_display_rows
        (bred_buffer_clear_screen config)
        0))
  match
    (truthy (string_write new_config.buffer))
      => (bred_breditor_config_with_buffer new_config (string_free new_config.buffer))
    (default) => do
      (bred_dead "bred_display" 1)
      (bred_new_breditor_config config.window_data)
    end
  end
end

fun bred_loop (config: BreditorConfig) BreditorConfig do
  let c: char <- (bred_read_char config)
  match
    (eq (bred_process_char config c) 0) => do
      let next_config: BreditorConfig <- (bred_display config)
      (bred_loop next_config)
    end
    (default)
      => config
  end
end

fun bred_process_char (config: BreditorConfig c: char) int do
  (unused config)
  match
    (falsey c)
      => 0
    (truthy (iscntrl c)) => match
      (eq c QUIT) => do
        (bred_clear_screen)
        1
      end
      (eq c ENTER) => do
        (printf "\r\n")
        0
      end
      (default) => do
        (printf "\r\nUnhandled control sequence: %d\r\n" c)
        0
      end
    end
    (eq c QUIT) => do
      (bred_clear_screen)
      1
    end
    (default) => do
      (printf "%c" c)
      (fflush stdout)
      0
    end
  end
end

fun bred_read_char (config: BreditorConfig) char do
  mut input: ptr<char> <- config.input
  (memset input 0 1)
  let read_result: int <- (read STDIN_FILENO input 1)
  match
    (and
      (eq read_result (neg 1))
      (not (eq errno EAGAIN)))
      => (bred_dead "read" 1)
    (default)
      => (unsafe_index input 0)
  end
end

fun bred_setup () BreditorConfig do
  (bred_raw_enable
    (bred_new_breditor_config
      (bred_get_window_data)))
end

fun main () int do
  mut config: BreditorConfig <- (bred_setup)
  let end_config: BreditorConfig <- (bred_loop config)
  (bred_free_breditor_config end_config)
  0
end

importc "stdlib.h"
importc "string.h"
importc "unistd.h"

type String struct is
  data: cstring
  len: size_t
end

type StringErr struct is
  message: String
end

type StringValueKind enum is
  Value <- 0
  Error <- 1
end

type StringValueUnion union is
  value: char
  error: StringErr
end

type StringValue struct is
  inner: StringValueUnion
  kind: StringValueKind
end

let STRING_ERR_FORMAT <- "%.*s"

fun string_new_err (message: String) StringErr do
  {StringErr
    message <- message
  }
end

? Appends a string to an existing string, returning an entirely new string.
?   Ownership: Does not take ownership of `a` data, but copies it
fun string_append (a: String b: String) String do
  let new_len: size_t <- (add a.len b.len)
  mut new_data: cstring <- (malloc (mul new_len (sizeof char)))
  match
    (falsey new_data) => (string_new_empty)
    (default) => do
      ? Copy first string
      (memcpy new_data a.data a.len)
      ? Copy second string
      (memcpy (point (unsafe_index new_data a.len)) b.data b.len)
      (string_new new_data new_len)
    end
  end
end

? Appends a string to an existing string, in-place
?   Ownership: Does take ownership of `a` data
fun string_append_fast (a: String b: String) String do
  let new_len: size_t <- (add a.len b.len)
  mut new_data: cstring <- (realloc a.data new_len)
  match
    (falsey new_data) => (string_new_empty)
    (default) => do
      ? Copy second string
      (memcpy (point (unsafe_index new_data a.len)) b.data b.len)
      (string_new new_data new_len)
    end
  end
end

? Compares string a and b, returning values as (memcmp) does
fun string_compare (a: String b: String) int do
  match
    (lt a.len b.len) => (memcmp a.data b.data a.len)
    (default) => (memcmp a.data b.data b.len)
  end
end

fun string_equals (a: String b: String) bool do
  (eq 0 (string_compare a b))
end

fun string_free (str: String) String do
  match
    (truthy str.data) => do
      let data: cstring <- str.data
      (delete data)
      (string_new_empty)
    end
    (default) => (string_new_empty)
  end
end

fun string_from_cstring (data: ptr<char>) String do
  let len: size_t <- (strlen data)
  mut copied_data: ptr<char> <- (malloc len)
  (memcpy copied_data data len)
  {String
    data <- data
    len <- (strlen data)
  }
end

fun string_from_ccstring (data: cstring) String do
  (string_from_cstring (cast cstring data))
end

fun string_index (str: String index: size_t) StringValue do
  match
    (gt str.len index) =>
      {StringValue
        inner <- {StringValueUnion
          value <- (unsafe_index str.data index)
        }
        kind <- #StringValueKind.Value
      }
    (default) => do
      (fprintf
        stderr
        "String: { data: %p, len: %ld } was improperly indexed with index: %ld\n"
        str.data
        str.len
        index)
      {StringValue
        inner <- {StringValueUnion
          error <- {StringErr
            message <- (string_from_ccstring "String was improperly indexed")
          }
        }
        kind <- #StringValueKind.Error
      }
    end
  end
end

fun string_new (data: ptr<char> len: size_t) String do
  {String
    data <- data
    len <- len
  }
end

fun string_new_empty () String do
  (string_new NULL 0)
end

fun string_write (str: String) int do
  (write STDOUT_FILENO str.data str.len)
end
